:Namespace core
    ∇ rpt←{codes}RunTests path;M;S;V;exec;folder;tst;tsts
    ⍝  e.g. RunTests 'tests/core.csv'
      V←7 10 3 8 4 7 6 10 7 2 8 9 2 1 3 7 8 1 7 5
      M←4 5⍴V

      exec←{
        ⍝  0 1 2 <=> passed, failed, broke
          0::2 ⍺ ⍵
          z r←⍎¨⍺ ⍵
        ⍝  645∊⎕DR¨z r:(≢/⍕¨z r)(⍺)(⍵)
          (z≢r)(⍺)(⍵)
      }
      folder←⊃⎕NPARTS 4⊃5179⌶⊃⎕SI
      tsts←⊃⎕CSV(folder,path)''⍬ 1
      rpt←0 3⍴0 '' ''
      :For tst :In ↓tsts
          rpt⍪←⊃exec/tst
      :EndFor

      :If 2≠⎕NC'codes'
          codes←0 1 2
      :EndIf
      rpt←rpt⌿⍨rpt[;1]∊codes
    ∇
      asc←{
        ⍝   ascending sort
          1=≢⍴⍵:⍵[⍋⍵]
          ⍉(∇⍤1)⍉⍵
      }
      desc←{
        ⍝   descending sort
          1=≢⍴⍵:⍵[⍒⍵]
          ⍉(∇⍤1)⍉⍵
      }
      max←{
        ⍝   maximum values
          ⌈⌿⍵
      }
    mean←{(+⌿⍵)÷≢⍵}     ⍝ arithmetic mean
      med←{ ⍝  median
          srtd←srt ⍵
          ×1|hlf←(≢⍵)÷2:(⌈hlf)⌷srtd
          ⊃2÷⍨+/(hlf+0 1)⌷¨⊂srtd
      }
      min←{
        ⍝   minimum values
          ⌊⌿⍵
      }
    mode←{1=≢⍴⍵:{⍺/⍨⍵=⌈/⍵}/⊂[1]{⍺,≢⍵}⌸⍵ ⋄ ∇¨⊂[1]⍵} ⍝ sample mode
    sd←{(var ⍵)*÷2} ⍝   population standard deviation
    sqr←{⍵*2}
    sqrt←{⍵*÷2}
    srt←{1=≢⍴⍵:⍵[⍋⍵] ⋄ ⍉(∇⍤1)⍉⍵}
      sum←{
        ⍝   arithmetic sum
          +⌿⍵
      }
    var←{mean 2*⍨ ⍵(-⍤1)mean ⍵}    ⍝  variance
:EndNamespace
